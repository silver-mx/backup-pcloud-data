plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.graalvm.buildtools.native' version '0.9.27'
}

group = 'dns.backup'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.modulith:spring-modulith-bom:1.0.2'
        mavenBom 'org.jmolecules:jmolecules-bom:2023.1.0'
    }
}

dependencies {
    // PROD dependencies
    implementation 'org.springframework.modulith:spring-modulith-starter-core'
    implementation 'org.springframework.modulith:spring-modulith-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // Unsure if needed
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    //runtimeOnly 'com.tngtech.archunit:archunit:1.2.0'

    // TEST dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.modulith:spring-modulith-starter-test'

    // DEV dependencies
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
}

tasks.named('bootBuildImage') {
    /*
    The plugin uses packeto, which defaults to Java 17 support as explained
    in https://paketo.io/docs/howto/java/#jvm-default-version. To build with Java 21
    it is needed to specify to bump manually the buildpack version according to
    https://github.com/paketo-buildpacks/bellsoft-liberica/releases */
    buildpacks = ['gcr.io/paketo-buildpacks/java-native-image', 'gcr.io/paketo-buildpacks/bellsoft-liberica:10.4.2']
    environment['BP_NATIVE_IMAGE'] = 'true'
    environment['BP_JVM_VERSION'] = '21.0.0'
    //environment['BP_NATIVE_IMAGE_BUILD_ARGUMENTS'] = '--verbose'
}

tasks.named('test') {
    useJUnitPlatform()
}
